// ************************
// B_TeachPlayerTalentRunes
// ************************

func int B_TeachPlayerTalentScrolls (var C_NPC slf, var C_NPC oth, var int spell)
{
	// ------ Kosten festlegen ------
	var int kosten;
	kosten = B_GetLearnCostTalent(oth, NPC_TALENT_SCROLLS, spell);
	
	
	//EXIT IF...
	
	// ------ Player hat zu wenig Lernpunkte ------
	if (oth.lp < kosten)
	&& (Mod_Schwierigkeit != 4)
	{
		PrintScreen	(PRINT_NotEnoughLearnPoints, -1,-1, FONT_ScreenSmall ,2);
		B_Say (slf, oth, "$NOLEARNNOPOINTS");
		
		return FALSE;
	}
	else if (Npc_HasItems(hero, ItMi_Gold) < kosten)
	&& (Mod_Schwierigkeit == 4)
	{
		PrintScreen	(PRINT_NotEnoughGold, -1, -1, FONT_Screen, 2);
		B_Say (slf, oth, "$NOLEARNGOLD");
		
		return FALSE;
	};
	
			
	// FUNC
				
	// ------ Lernpunkte abziehen ------	
	if (Mod_Schwierigkeit != 4)
	{
		oth.lp = oth.lp - kosten;
	}
	else
	{
		Npc_RemoveInvItems	(hero, ItMi_Gold, kosten);
	};
	
	Log_CreateTopic (TOPIC_TalentScrolls,LOG_NOTE);
	B_LogEntry (TOPIC_TalentScrolls,"In order to create a spell roll, I need a rune of the magic direction, ideally the spell I want to create as a spell roll, the ingredients for the spell roll, a sheet of paper and an empty rune stone as a transfer medium. If I want to make stronger spell rolls for which I don't have a rune, I also needed the knowledge and ingredients for the weaker spells until I had a rune. For example, all the ingredients for a fireball are required for a fire arrow.");
	
	PrintScreen			(PRINT_LearnScrolls, -1, -1, FONT_Screen, 2);
	
	// ------ bei jeder Rune: Runen-Talent lernen (programmvariable, wird nur zur Ausgabe in StatusScreen benutzt) ------
	Npc_SetTalentSkill 	(oth, NPC_TALENT_SCROLLS, 1);
	return TRUE;
};
